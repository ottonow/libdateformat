buildscript {
    ext {
        kotlin_version = '1.2.61'
        springBootVersion = '2.0.4.RELEASE'
        dokka_version = "0.9.17"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'de.ottonow'
archivesBaseName = 'relaxed-date-time'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom {
                name = 'relaxed-date-time'
                description = 'Relaxed date time parser and converter for Spring Boot 2'
                url = 'https://github.com/ottonow/relaxed-date-time'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/ottonow/relaxed-date-time/blob/master/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/ottonow/relaxed-date-time'
                    connection = 'scm:https://github.com/ottonow/relaxed-date-time.git'
                    developerConnection = 'scm:git://github.com/ottonow/relaxed-date-time.git'
                }
                developers {
                    developer {
                        id = 'kevcodez'
                        name = 'Kevin Gr√ºneberg'
                        email = 'k.grueneberg1994@gmail.com'
                    }
                }
            }


        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}